package Drone
public
	with Physics, Supply_Chain, Resolute_Functions;
	
	system Complete
	end Complete;
	
	system implementation Complete.Orig
		subcomponents
			Ground_Station: system Ground_Station;
			Drone: system Drone.Quad_Orig;
		connections
			Mission_Data: port Ground_Station.Mission_Data -> Drone.Mission_Data;
			Position_Data: port Drone.Position_Data -> Ground_Station.Position_Data;
			Video_Data: port Drone.Video_Data -> Ground_Station.Video_Data;
		flows
			Video_Flow: end to end flow Drone.Video_Source -> Video_Data -> Ground_Station.Video_Sink;
		annex resolute {**
			prove(Weight_Under_Budget(this.Drone))
			prove(Desired_Battery_Life_Achieved(this.Drone.Flight_System.Battery))
			prove(Supply_Chain_Attacks_Mitigated())
		**};
	end Complete.Orig;
	
	system implementation Complete.Encrypted_Video
		subcomponents
			Ground_Station: system Ground_Station;
			Drone: system Drone.Quad_Encrypted_Video;
		connections
			Mission_Data: port Ground_Station.Mission_Data -> Drone.Mission_Data;
			Position_Data: port Drone.Position_Data -> Ground_Station.Position_Data;
			Video_Data: port Drone.Video_Data -> Ground_Station.Video_Data;
		flows
			Video_Flow: end to end flow Drone.Video_Source -> Video_Data -> Ground_Station.Video_Sink;
		annex resolute {**
			prove(Weight_Under_Budget(this.Drone))
			prove(Desired_Battery_Life_Achieved(this.Drone.Flight_System.Battery))
			prove(Supply_Chain_Attacks_Mitigated())
		**};
	end Complete.Encrypted_Video;
	
	system Ground_Station
		features
			Mission_Data: out data port;
			Position_Data: in data port;
			Video_Data: in data port;
		flows
			Video_Sink: flow sink Video_Data;
	end Ground_Station;
	
	system Drone
		features
			Mission_Data: in data port;
			Position_Data: out data port;
			Video_Data: out data port;
		flows
			Video_Source: flow source Video_Data;
		properties
			-- 1/2 the total thrust of our motors
			Physics::Weight_Budget => 3672.0 g;
	end Drone;
	
	system implementation Drone.Quad_Orig
		subcomponents
			Flight_System: system Flight_System.I;
			Radio_Controller: device Radio_Controller.I;
		connections
			Mission_Data_External: port Mission_Data -> Radio_Controller.Mission_Data_External;
			Position_Data_External: port Radio_Controller.Position_Data_External -> Position_Data;
			Video_Data_External: port Radio_Controller.Video_Data_External -> Video_Data;
			
			Mission_Data_Internal: port Radio_Controller.Mission_Data_Internal -> Flight_System.Mission_Data;
			Position_Data_Internal: port Flight_System.Position_Data -> Radio_Controller.Position_Data_Internal;
			Video_Data_Internal: port Flight_System.Video_Data -> Radio_Controller.Video_Data_Internal;
		flows
			Video_Source: flow source Flight_System.Video_Source -> Video_Data_Internal
									-> Radio_Controller.Video_Flow -> Video_Data_External -> Video_Data;
	end Drone.Quad_Orig;
	
	system implementation Drone.Quad_Encrypted_Video
		subcomponents
			Flight_System: system Flight_System.I;
			Encryption_Module: device Encryption_Module.I;
			Radio_Controller: device Radio_Controller.I;
		connections
			Mission_Data_External: port Mission_Data -> Radio_Controller.Mission_Data_External;
			Position_Data_External: port Radio_Controller.Position_Data_External -> Position_Data;
			Video_Data_External: port Radio_Controller.Video_Data_External -> Video_Data;
			
			Mission_Data_Internal: port Radio_Controller.Mission_Data_Internal -> Flight_System.Mission_Data;
			Position_Data_Internal: port Flight_System.Position_Data -> Radio_Controller.Position_Data_Internal;
			
			Video_Data_Internal_Unencrypted: port Flight_System.Video_Data -> Encryption_Module.Plaintext;
			Video_Data_Internal_Encrypted: port Encryption_Module.Ciphertext -> Radio_Controller.Video_Data_Internal;
		flows
			Video_Source: flow source Flight_System.Video_Source -> Video_Data_Internal_Unencrypted
									-> Encryption_Module.Encrypt -> Video_Data_Internal_Encrypted
									-> Radio_Controller.Video_Flow -> Video_Data_External -> Video_Data;
	end Drone.Quad_Encrypted_Video;
	
	system Flight_System
		features
			Mission_Data: in data port;
			Position_Data: out data port;
			Video_Data: out data port;
		flows
			Video_Source: flow source Video_Data;
	end Flight_System;
	
	system implementation Flight_System.I
		subcomponents
			Flight_Controller: system Flight_Controller.I;
			Mission_Controller: system Mission_Controller.I;
			Vision_Controller: system Vision_Controller.I;
			
			GPS: device GPS.I;
			Camera_System: system Camera_System.I;
			
			Propulsion: system Propulsion.Quad;
			Battery: device Battery.Zeee_6S_Lipo;
		connections
			Mission_Data_External: port Mission_Data -> Mission_Controller.Mission_Data;
			Position_Data_External: port Flight_Controller.Position_Data -> Position_Data;
			Processed_Video_External: port Vision_Controller.Processed_Video -> Video_Data;
			
			GPS_Data: port GPS.GPS_Data -> Flight_Controller.GPS_Data;
			Waypoint_Data: port Mission_Controller.Waypoint_Data -> Flight_Controller.Waypoint_Data;
			Position_Data_To_Mission_Controller: port Flight_Controller.Position_Data -> Mission_Controller.Position_Data;
			ESC_Data: port Flight_Controller.ESC_Control -> Propulsion.ESC_Control;
			
			Raw_Video: port Camera_System.Raw_Video -> Vision_Controller.Raw_Video;
			Processed_Video_Internal: port Vision_Controller.Processed_Video -> Mission_Controller.Processed_Video;
			
			Pan: port Vision_Controller.Pan -> Camera_System.Pan;
			Tilt: port Vision_Controller.Tilt -> Camera_System.Tilt;
			Zoom: port Vision_Controller.Zoom -> Camera_System.Zoom;
		flows
			Video_Source: flow source Camera_System.Video_Source -> Raw_Video
									-> Vision_Controller.Video_Processing -> Processed_Video_External -> Video_Data;
		properties
			Physics::Power_Source => reference(Battery) applies to
				Propulsion.Left_Tractor, Propulsion.Right_Tractor, Propulsion.Left_Pusher, Propulsion.Right_Pusher;
	end Flight_System.I;
	
	system Flight_Controller
		features
			Position_Data: out data port;
			Waypoint_Data: in data port;
			GPS_Data: in data port;
			ESC_Control: out data port;
	end Flight_Controller;
	
	system implementation Flight_Controller.I
		properties
			Supply_Chain::Provenance => (France);
			Physics::Weight => 52.0 g;
	end Flight_Controller.I;
	
	system Mission_Controller
		features
			Mission_Data: in data port;
			Position_Data: in data port;
			Waypoint_Data: out data port;
			Processed_Video: in data port;
		flows
			Video_Sink: flow sink Processed_Video;
	end Mission_Controller;
	
	system implementation Mission_Controller.I
		properties
			Supply_Chain::Provenance => (USA);
			Physics::Weight => 86.0 g;
	end Mission_Controller.I;
	
	system Vision_Controller
		features
			Raw_Video: in data port;
			Processed_Video: out data port;
			Pan: out data port;
			Tilt: out data port;
			Zoom: out data port;
		flows
			Video_Processing: flow path Raw_Video -> Processed_Video;
	end Vision_Controller;
	
	system implementation Vision_Controller.I
		properties
			Supply_Chain::Provenance => (USA);
			Physics::Power_Draw => 10.0 W; -- The Jetson Nano draws 5V at 2A through micro-USB
			Physics::Weight => 252.3 g; -- Again, based on the Jetson Nano
	end Vision_Controller.I;
	
	device Encryption_Module
		features
			Plaintext: in data port;
			Ciphertext: out data port {Supply_Chain::Provided_Assurances => (Confidentiality);};
		flows
			Encrypt: flow path Plaintext -> Ciphertext;
	end Encryption_Module;
	
	device implementation Encryption_Module.I
		properties
			Supply_Chain::Provenance => (USA, Singapore);
			Physics::Weight => 76.0 g;
	end Encryption_Module.I;
	
	device Radio_Controller
		features
			Mission_Data_External: in data port;
			Mission_Data_Internal: out data port;
			
			Video_Data_Internal: in data port;
			Video_Data_External: out data port {Supply_Chain::Provided_Assurances => (Confidentiality);};
			
			Position_Data_Internal: in data port;
			Position_Data_External: out data port {Supply_Chain::Provided_Assurances => (Confidentiality);};
		flows
			Video_Flow: flow path Video_Data_Internal -> Video_Data_External;
	end Radio_Controller;
	
	device implementation Radio_Controller.I
		properties
			Supply_Chain::Provenance => (North_Korea);
			Physics::Weight => 130.0 g;
	end Radio_Controller.I;
	
	system Camera_System
		features
			Raw_Video: out data port {Supply_Chain::Required_Assurances => (Confidentiality);};
			Pan: in data port;
			Tilt: in data port;
			Zoom: in data port;
		flows
			Video_Source: flow source Raw_Video;
	end Camera_System;
	
	system implementation Camera_System.I
		properties
			Supply_Chain::Provenance => (Singapore, USA);
			Physics::Weight => 156.0 g;
	end Camera_System.I;
	
	device GPS
		features
			GPS_Data: out data port;
	end GPS;
	
	device implementation GPS.I
		properties
			Supply_Chain::Provenance => (France);
			Physics::Weight => 11.0 g;
	end GPS.I;
	
	system Propulsion
		features
			ESC_Control: in data port;
	end Propulsion;
	
	system implementation Propulsion.Quad
		subcomponents
			ESC: device ESC.Hobbywing_200A;
			
			Left_Tractor: device Propeller.Five33_Headsup_2207_5x4x3;
			Left_Pusher: device Propeller.Five33_Headsup_2207_5x4x3;
			
			Right_Tractor: device Propeller.Five33_Headsup_2207_5x4x3;
			Right_Pusher: device Propeller.Five33_Headsup_2207_5x4x3;
		connections
			ESC_Data_In: port ESC_Control -> ESC.Control;
			Speed1: port ESC.Speed1 -> Left_Tractor.Speed;
			Speed2: port ESC.Speed2 -> Right_Tractor.Speed;
			Speed3: port ESC.Speed3 -> Left_Pusher.Speed;
			Speed4: port ESC.Speed4 -> Right_Pusher.Speed;
	end Propulsion.Quad;
	
	device ESC
		features
			Control: in data port;
			Speed1: out data port;
			Speed2: out data port;
			Speed3: out data port;
			Speed4: out data port;
	end ESC;
	
	device implementation ESC.Hobbywing_200A
		-- https://www.amainhobbies.com/hobbywing-platinum-pro-200a-hv-v4.1-esc-sbec-hwa30209101/p-qtaqt2uqhzzxactz
		properties
			Physics::Weight => 325.0 g;
	end ESC.Hobbywing_200A;
	
	device Propeller
		features
			Speed: in data port;
	end Propeller;
	
	device implementation Propeller.Five33_Headsup_2207_5x4x3
		properties -- From https://www.miniquadtestbench.com/assets/components/motordata/motorinfo.php?uid=287
			Physics::Current_Draw => 23.88 A; -- Current at 75% thrust (1336 g)
			Physics::Weight => 29.3 g;
	end Propeller.Five33_Headsup_2207_5x4x3;
	
	device Battery
	end Battery;
	
	device implementation Battery.Zeee_6S_Lipo
		properties
			-- Based a pair of Zeee 6S Lipos wired in parallel: https://www.amazon.com/dp/B07ZV5DDWQ
			Physics::Weight => 1710.0 g;
			Physics::Max_Charge => 12000.0 mAh;
			Physics::Nominal_Output_Voltage => 22.2 V;
			Physics::Desired_Battery_Life => 0.1 hr;
	end Battery.Zeee_6S_Lipo;
	
end Drone;